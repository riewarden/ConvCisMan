===after_glow

The two of you sit next to each other in{room: comfortable} silence. Toby looks across at you{not sex:, a little concerned, clearly trying out various responses in his head}.
{sex == false and room == false: ->afterdark} {sex == true: ->postcoit|->two}

=postcoit
"Was that...ok? I was worried that it was...{hard.org:too intense}{gently.org:too vanilla}." 

+ "It was hot."
    Surprisingly, he smiles, a little embarrassed. "Thanks." 
    He swallows nervously. "I, uh...I nearly cried when I came. I just never thought-"
    He stops, his words catching in his throat. He takes a deep breath.
    "Yep."
+ (time)"I...need some time to think about it."
 He nods. "Sure. No skin off my nose. I hope you're ok".

- ->two

=two
He puts his hand next to yours.

+ [Take it.]You take it.
    His thumb moves across your palm.
+ Leave it.

- More silence.

"I had a really nice time tonight." He says, after a while.
{not postcoit.time:

    "{sex == false:Don't worry about the sex. I don't mean the sex.|Not just the sex, mind.}"
    
    {sex: "And the sex was great. That's a weight off my mind. Just knowing I can do it with a cis guy now. Obviously, I like you a lot, but this has also been a very useful experience for me on a practical level. Getting over the first-time nerves. Is that selfish? I tried to fuck you in an un-selfish way. At the very least. Well, whatever happened, I think we both enjoyed it. That's something, right?" <br> He grins at you.}
}
+ He sighs, happily.->three

=three
<br>
He stares into the middle distance for a while, before he speaks again.

"This meant a lot to me. To connect, with another guy, like this. I know I've been talking your goddamn ear off, and ordering you around, but, I just don't...hear about things like this happening. Or, read about them, or see them on TV."

He looks at you with a deep fondness.

"You listened to me. You were interested to know about me, about how my body works, how I love, and fuck.{not sex: And when you didn't want to take it any further, you were honest about it. That's so important to me.}"

* {sex} He shuffles closer, and puts his head on your shoulder. 
* {not sex} He hesitantly brushes your shoulder with his hand.
- ->threepoint

=threepoint

"So much of the time, I feel like I'm not supposed to even talk about what sex means to me. But, you made me feel really special, and made me feel like, it is ok. And, I can carry that with me now. I thought the sex was the most important part but I think, it was just nice to be reminded I can talk about things and just...be listened to."

+ He strokes your hair, and looks into your eyes.->four

=four
<br>
"I only hope I did something similar for you."

->END

===afterdark
"If you don't want to, that's ok." He says, and smiles at you reassuringly.

He looks away. 

Another silence.

"So..." He says, hesitantly, "...What were all the questions for?"
+ "Well...you didn't tell me to stop asking...So..."
    ->questions
+ "I do like you. In some way. This is just...a lot."
    ->after_glow.two
    
+ "I...don't know."
    He sighs. 
    
    "Well...I can't tell you why you asked them. I guess the best I can offer is that you either like me, and you're just a bit overwhelmed, which is okay, I wouldn't be offended by that...or, you wanted to satisfy your curiosity, at the expense of my privacy. And my feelings."
    
    He looks at you.
    
    "And...Those two options are the only things in my imagination. Maybe it's something else. But, I'm just trying not to overthink this."
    
    He looks at the carpet. Another long silence.
    
    "Look, when you've figured it out, just...let me know."
    
    He puts his face in his hands.
    
    "God, I'm exhausted. I hope you're doing ok."
    
    ++ "You...didn't tell me to stop asking questions."->questions
    ++ "Ok. I think I like you in some way, but I'm not sure what that is yet."->after_glow.two
    ++ "I'm going to go away and think about this."->go_away
    
===questions
    He nods, and grits his teeth.

    "Yeah. I didn't. I thought... It was part of it. When you're asking a person... Very intimate and really quite invasive questions about how they have sex, it sends a certain message."
    
    + He clasps his hands.->two
    
=two
<br>
    "Now, yes, communication and explicit consent is everything, especially in sexual relationships. But, why is it that I don't believe that you would have excused this whole situation as 'just asking questions', if I was cis?"
    
    + He glares at you.->three
=three  
<br>
    "Here's what I think you're saying. You're saying that it was actually MY fault for answering YOUR personal questions about me, and trusting that you had an actual REASON for asking them. 
    
    His voice sounds tightly coiled.
    
    "I think, if I was cis, you would realise that you actually can't just ask a person a bunch of sexual questions like that, and then act like the other person is being completely unreasonable by assuming you had a sexual reason for doing so."
    
    + He grabs his head in frustration.->four
=four
<br>
    "Even now, even now! I'm still wondering if I'm the one who's completely wrong." 
    
    He holds up his hands in bewilderment, and you can see him shaking.
    
    "Am I just stupid, for telling a guy that I would explain to him how I have sex, but only if he comes into my house? And, then, he asks multiple questions about how I have sex, and how my genitals work, and how I feel about that. He then asks me to explain how I would have sex with him. He then says 'Actually, this is not what this is about. I was in fact, Just Asking Questions'. 
    
    + He turns to you, suddenly.->five
    
=five
<br>
    "Am I supposed to just wait patiently, for dumb fucking cis people to ask me any question about my body that they like, so I can bare my fucking soul, for their benefit, and, just be, 'okay', when there's absolutely nothing in it for me? Not even a simple conversation where I'm not being lied to, the entire fucking time? I don't need you to have sex with me. I just want you to be honest about why you're bothering to talk to me."
    
    + He breathes heavily.->six

=six
<br>
    "I answered your questions, because I thought you cared. That you actually wanted to...know me better. {j_g:And, well, you had me completely fooled. }You were just asking me all those things because...because my sex life is some interesting trivia. Well, why learn it from me? A person? You can learn trivia, fucking anywhere!"
    
    He stands up and points at the door, he's seething with rage.
    
    "Just get out. Just fucking get out."
    
    ->END
    
===go_away
  He nods.
  
  "Ok. If that's how you feel."
  
  He leads you to the door.
  
  "You will...let me know? If, you want to try again sometime, right? I- well. I can't force you to. It's your decision. You just seem nice, and I hope I'm right."
  
  He waves.
  
  "See you."
    
->END